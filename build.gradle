plugins {
  id 'java'
  id 'application'

  // To create a fat jar build/libs/multi-version-control-all.jar, run: ./gradlew shadowJar
  id 'com.github.johnrengelman.shadow' version '5.1.0'

  // Run the Error Prone linter.
  id 'net.ltgt.errorprone-base' version '0.0.16'

  // Checker Framework pluggable type-checking
  id 'org.checkerframework' version '0.4.8' apply false
}

repositories {
  mavenCentral()
}

dependencies {
  // https://mvnrepository.com/artifact/org.tmatesoft.svnkit/svnkit
  implementation group: 'org.tmatesoft.svnkit', name: 'svnkit', version: '1.10.1'

  // https://mvnrepository.com/artifact/org.ini4j/ini4j
  implementation group: 'org.ini4j', name: 'ini4j', version: '0.5.4'

  implementation 'org.apache.commons:commons-exec:1.3'
  implementation 'org.plumelib:options:1.0.3'
  implementation 'org.plumelib:plume-util:1.0.10'

  compileOnly group: 'org.plumelib', name: 'require-javadoc', version: '0.1.2'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'org.plumelib.multiversioncontrol.MultiVersionControl'

/// Checker Framework pluggable type-checking

apply plugin: 'org.checkerframework'

checkerFramework {
  checkers = [
    'org.checkerframework.checker.index.IndexChecker',
    'org.checkerframework.checker.lock.LockChecker',
    'org.checkerframework.checker.nullness.NullnessChecker',
    'org.checkerframework.checker.regex.RegexChecker'
  ]
  extraJavacArgs = [
    '-Werror',
    '-AcheckPurityAnnotations',
    '-ArequirePrefixInWarningSuppressions',
    '-AwarnUnneededSuppressions'
  ]
}

// To use a local-built Checker Framework, run gradle with "-PcfLocal".
if (project.hasProperty("cfLocal")) {
  def cfHome = String.valueOf(System.getenv("CHECKERFRAMEWORK"))
  dependencies {
    compileOnly files(cfHome + "/checker/dist/checker-qual.jar")
    testCompileOnly files(cfHome + "/checker/dist/checker-qual.jar")
    checkerFramework files(cfHome + "/checker/dist/checker.jar")
    checkerFrameworkAnnotatedJDK files(cfHome + "/checker/dist/jdk8.jar")
  }
}

/// Javadoc

// Turn Javadoc warnings into errors.
javadoc {
  options.addStringOption('Xwerror', '-Xdoclint:all')
  options.addStringOption('private', '-quiet')
}

task javadocWeb(type: Javadoc) {
  source = sourceSets.main.allJava
  destinationDir = file("/cse/web/research/plumelib/multiversioncontrol/api")
  classpath = project.sourceSets.main.compileClasspath
}

task requireJavadoc(type: Javadoc) {
  description = 'Ensures that Javadoc documentation exists.'
  destinationDir.deleteDir()
  source = sourceSets.main.allJava
  classpath = project.sourceSets.main.compileClasspath
  options.memberLevel = JavadocMemberLevel.PRIVATE
  options.docletpath = project.sourceSets.main.compileClasspath as List
  options.doclet = "org.plumelib.javadoc.RequireJavadoc"
}
check.dependsOn requireJavadoc

task updateUserOptions(type: Javadoc, dependsOn: 'assemble') {
  description "Updates printed documentation of command-line arguments."

  source = sourceSets.main.allJava
  classpath = project.sourceSets.main.compileClasspath
  options.memberLevel = JavadocMemberLevel.PRIVATE
  options.docletpath = project.sourceSets.main.runtimeClasspath as List
  options.doclet = "org.plumelib.options.OptionsDoclet"
  options.addStringOption("docfile", "${projectDir}/src/main/java/org/plumelib/multiversioncontrol/MultiVersionControl.java")
  options.addStringOption("format", "javadoc")
  options.addStringOption("i", "-quiet")
  title = ""
}

/// Emacs support

/* Make Emacs TAGS table */
task tags(type: Exec) {
  description "Run etags to create an Emacs TAGS table"
  commandLine "bash", "-c", "find src/ -name '*.java' | sort | xargs etags"
}
