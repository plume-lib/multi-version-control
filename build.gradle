// Checker Framework pluggable type-checking
buildscript {
  repositories {
    mavenLocal()
  }
  
  dependencies {
    classpath 'com.jaredsburrows:gradle-checker-framework-plugin:0.2.3-SNAPSHOT'
  }
}

plugins {
  id 'application'
  id 'java'
  id 'com.github.johnrengelman.shadow' version '4.0.2'
}

apply plugin: 'com.jaredsburrows.checkerframework'

repositories {
  mavenCentral()
}

dependencies {
  // https://mvnrepository.com/artifact/org.tmatesoft.svnkit/svnkit
  implementation group: 'org.tmatesoft.svnkit', name: 'svnkit', version: '1.9.3'

  // https://mvnrepository.com/artifact/org.ini4j/ini4j
  implementation group: 'org.ini4j', name: 'ini4j', version: '0.5.4'

  implementation 'org.apache.commons:commons-exec:1.3'
  implementation 'org.plumelib:options:1.0.1'
  implementation 'org.plumelib:plume-util:1.0.5'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Define the main class for the application
mainClassName = 'org.plumelib.multiversioncontrol.MultiVersionControl'

// Checker Framework pluggable type-checking
checkerFramework {
  checkers = [
    'org.checkerframework.checker.index.IndexChecker',
    'org.checkerframework.checker.lock.LockChecker',
    'org.checkerframework.checker.nullness.NullnessChecker',
    'org.checkerframework.checker.regex.RegexChecker'
  ]
  extraJavacArgs = [
    '-AcheckPurityAnnotations'
  ]
}


/// Javadoc

// Turn Javadoc warnings into errors.
javadoc {
  options.addStringOption('Xwerror', '-Xdoclint:all')
  options.addStringOption('private', '-quiet')
}

task javadocWeb(type: Javadoc) {
  source = sourceSets.main.allJava
  destinationDir = file("/cse/web/research/plumelib/multiversioncontrol/api")
  classpath = project.sourceSets.main.compileClasspath
}

configurations {
  requireJavadoc  // ensure that all Java elements have Javadoc documentation.
}
dependencies {
  implementation group: 'org.plumelib', name: 'require-javadoc', version: '0.1.0'
}
task requireJavadoc(type: Javadoc) {
  description = 'Ensures that Javadoc documentation exists.'
  destinationDir.deleteDir()
  source = sourceSets.main.allJava
  classpath = project.sourceSets.main.compileClasspath
  options.memberLevel = JavadocMemberLevel.PRIVATE
  options.docletpath = project.sourceSets.main.compileClasspath as List
  options.doclet = "org.plumelib.javadoc.RequireJavadoc"
}


/// Emacs support

/* Make Emacs TAGS table */
task tags(type: Exec) {
  description "Run etags to create an Emacs TAGS table"
  commandLine "bash", "-c", "find src/ -name '*.java' | sort | xargs etags"
}
